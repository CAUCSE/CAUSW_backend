// flyway-config.gradle

// 1. í™˜ê²½(env) ì„¤ì •
def env = project.hasProperty("env") ? project.property("env") : "local"

// 2. .env.${env} íŒŒì‹±
def envFile = project.rootProject.file(".env.${env}")
if (envFile.exists()) {
    project.logger.lifecycle("ğŸ”§ Loading to project.ext from .env.${env}")
    envFile.eachLine { line ->
        def trimmedLine = line.trim()
        if (!trimmedLine || trimmedLine.startsWith('#')) return

        def (key, value) = line.split('=', 2)
        if (key && value) {
            project.ext.set(key.trim(), value.trim())
        }
    }
}

/* 3. í™˜ê²½ë³€ìˆ˜ ì„¤ì •
 *
 * ìš°ì„ ìˆœìœ„
 * (1) Gradle -P ì˜µì…˜ (e.g. ./gradlew flywayInfo -PDB_URL=... -PDB_USERNAME=... -PDB_PASSWORD=...)
 * (2) .env.{env}
 * (3) í™˜ê²½ë³€ìˆ˜ ì„¤ì • ìƒëµ
 */
def dbUrl = project.hasProperty("DB_URL") ? project.property("DB_URL") : null
def dbUsername = project.hasProperty("DB_USERNAME") ? project.property("DB_USERNAME") : null
def dbPassword = project.hasProperty("DB_PASSWORD") ? project.property("DB_PASSWORD") : null

if (!dbUrl || !dbUsername || !dbPassword) {
    logger.lifecycle("Skip Flyway configuration in Gradle: required DB properties not found.")
    return
}

// 4. Flyway ê¸°ë³¸ ì„¤ì •
flyway {
    url = dbUrl
    user = dbUsername
    password = dbPassword
    locations = ["filesystem:src/main/resources/db/migration"]

    // 5. í™˜ê²½ë³„ ì¶”ê°€ ì„¤ì •
    switch(env) {
        case 'dev':
            logger.lifecycle("ğŸš€ Applying DEV Flyway configuration")
            outOfOrder = true
            ignoreMigrationPatterns = ['*:ignored']
            validateOnMigrate = true
            cleanDisabled = false
            baselineOnMigrate = true
            break

        case 'staging':
            logger.lifecycle("ğŸ¯ Applying STAGING Flyway configuration")
            outOfOrder = false
            validateOnMigrate = true
            cleanDisabled = true
            baselineOnMigrate = false
            break

        case 'prod':
            logger.lifecycle("ğŸ”’ Applying PRODUCTION Flyway configuration")
            outOfOrder = false
            validateOnMigrate = true
            cleanDisabled = true
            baselineOnMigrate = false
            // ignoreMigrationPatterns ì„¤ì • ì•ˆí•¨ (ë¬´ì‹œëœ ë§ˆì´ê·¸ë ˆì´ì…˜ í—ˆìš© ì•ˆí•¨)
            break

        case 'local':
        default:
            logger.lifecycle("ğŸ’» Applying LOCAL Flyway configuration")
            outOfOrder = true
            ignoreMigrationPatterns = ['*:ignored', '*:pending']
            validateOnMigrate = false
            cleanDisabled = false
            baselineOnMigrate = true
    }
}